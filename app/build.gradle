import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

static releaseTime(){
    SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd", Locale.CHINA)
    format.setTimeZone(TimeZone.getTimeZone("UTC"))
    return format.format(new Date())
}
android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.code.code1.application"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        flavorDimensions "versionCode"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // 多渠道打包时对每个渠道的apk进行重命名
            applicationVariants.all { variant->
                variant.outputs.all{ output->
                    def outputFile = output.outputFile
                    if (outputFile!=null && outputFile.name.endsWith(".apk")){
//                        outputFile.name = "AndroidDemo_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        // studio 3.0 以后outputFile.name为只读属性，需要改为如下写法
                        outputFileName = "AndroidDemo_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                    }
                }

            }
        }
    }

    productFlavors{
        xiaomi{}
        huawei{}
        baidu{}
        wandoujia{}
        yingyongbao{}
    }
    productFlavors.all {
        flavors -> flavors.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
